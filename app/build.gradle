import com.android.build.api.variant.FilterConfiguration
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.android.application'
    // AGP 9.0+ 自动处理 Kotlin 支持，不再需要 org.jetbrains.kotlin.android
    id 'kotlin-parcelize' 
}

def props = new Properties()
def propFile = rootProject.file('local.properties')
def isCI = System.getenv("CI") == "true"
if (propFile.exists()) {
    props.load(new FileInputStream(propFile))
}

android {
    namespace = 'com.cappielloantonio.tempo'
    compileSdk = 36
    buildToolsVersion = '36.0.0'

    defaultConfig {
        minSdkVersion 24
        targetSdk 35

        manifestPlaceholders["HARMONYOS_SUPPORT"] = "true"

        versionCode 26012501
        versionName '1.0.0.26012501'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_ALIGNED_16K=ON"
                cppFlags "-Wl,-z,max-page-size=16384"
            }
        }
    }

    androidComponents {
        onVariants(selector().all()) { variant ->
            variant.outputs.each { output ->
                def versionName = output.versionName.get()
//                def versionCode = output.versionCode.get()
                def buildType = variant.buildType
                def flavorName = variant.flavorName ?: ""
                def flavorPart = flavorName ? "${flavorName}-" : ""
                // 获取 ABI（如果是 universal 则为 null）
                def abi = output.getFilter(FilterConfiguration.FilterType.ABI)
                def abiPart = abi ? "${abi.identifier}-" : ""

                output.outputFileName.set(
                        "Tempus-F-${flavorPart}${buildType}-${abiPart}v${versionName}.apk"
                )
            }
        }
    }


    splits {
        abi {
            enable = true
            reset()
//            include 'arm64-v8a'
            if (props['RELEASE_ABI']) {
                include(props['RELEASE_ABI'].split(','))
            } else {
                include 'armeabi-v7a', 'arm64-v8a', 'x86_64'
            }
            universalApk = false
        }
    }

    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }

    flavorDimensions = ["default"]

    productFlavors {
        tempus {
            dimension = "default"
            applicationId 'cn.sffzh.tempus-f'
        }
        degoogled {
            dimension = "default"
            applicationId "cn.sffzh.degoogled.tempus-f"
        }
    }

    signingConfigs {
        release {
            if (props['RELEASE_KEY_STORE_FILE']) {
                storeFile file(props['RELEASE_KEY_STORE_FILE'])
                storePassword props['RELEASE_STORE_PASSWORD']
                keyAlias props['RELEASE_KEY_ALIAS']
                keyPassword props['RELEASE_KEY_PASSWORD']
            }else { // CI 构建：禁用所有签名
                enableV1Signing = false
                enableV2Signing = false
                enableV3Signing = false
                enableV4Signing = false
            }
        }
    }

    buildTypes {
        release {
            if (!isCI) {
                signingConfig = signingConfigs.release
            } else { // CI 环境不使用 signingConfig
                signingConfig = null
            }
            shrinkResources = true
            minifyEnabled = true
            debuggable = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // AGP 9.0+ 的现代 Kotlin 配置写法
    kotlin {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_17)
        }
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }
}

dependencies {
    // AndroidX
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.3.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.9.6'
    implementation 'androidx.navigation:navigation-ui-ktx:2.9.6'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.room:room-runtime:2.8.4'
    implementation 'androidx.core:core-splashscreen:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation "androidx.media:media:1.7.1"
    implementation 'androidx.core:core:1.17.0'
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation "androidx.dynamicanimation:dynamicanimation:1.1.0"
    implementation "androidx.documentfile:documentfile:1.1.0"
    implementation "androidx.security:security-crypto:1.1.0"
    implementation("androidx.datastore:datastore-preferences:1.2.0")

    implementation("com.google.crypto.tink:tink-android:1.20.0")

    // Material & Glide
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'com.github.bumptech.glide:glide:5.0.5'
    implementation 'com.github.bumptech.glide:annotations:5.0.5'
    annotationProcessor 'com.github.bumptech.glide:compiler:5.0.5'

    // Media3
    implementation 'androidx.media3:media3-session:1.9.0'
    implementation 'androidx.media3:media3-common:1.9.0'
    implementation 'androidx.media3:media3-exoplayer:1.9.0'
    implementation 'androidx.media3:media3-ui:1.9.0'
    implementation 'androidx.media3:media3-exoplayer-hls:1.9.0'
    tempusImplementation 'androidx.media3:media3-cast:1.9.0'

    annotationProcessor 'androidx.room:room-compiler:2.8.4'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.3.2'
    implementation 'com.squareup.retrofit2:converter-gson:3.0.0'

    compileOnly 'com.google.j2objc:j2objc-annotations:3.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
